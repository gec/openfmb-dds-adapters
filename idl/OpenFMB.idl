module openfmb {

module commonmodule {

	typedef string UuidType;

	// milliseconds since Jan 1 1970 UTC
	typedef long long DateTimeType;

	// These two produce the same structure but this way we control the name of the internal field
	//typedef octet HexBinary16[2];
	struct HexBinary16Type {
	    octet data[2];
	};
	//@top-level false

	module unitmultiplier {
		enum UnitMultiplierKind {
		    centi,
		    deci,
		    Giga,
		    kilo,
		    Mega,
		    milli,
		    micro,
		    nano,
		    none,
		    pico,
		    Tera
		};
	};

	module unitsymbol {
		enum UnitSymbolKind {
		    Amp,
		    deg,
		    degC,
		    degF,
		    Farad,
		    gram,
		    hour,
		    Henry,
		    Hz,
		    Joule,
		    meter,
		    m2,
		    m3,
		    min,
		    mph,
		    Newton,
		    none,
		    ohm,
		    Pa,
		    rad,
		    Siemens,
		    sec,
		    V,
		    VA,
		    VAh,
		    VAr,
		    VArh,
		    W,
		    Wh,
		    WPerM2,
		    wPerVA
		};
	};

	module flowdirection {
		enum FlowDirectionKind {
		    forward,
		    lagging,
		    leading,
		    net,
		    none,
		    q1minusQ4,
		    q1plusQ2,
		    q1plusQ3,
		    q1plusQ4,
		    q2minusQ3,
		    q2plusQ3,
		    q2plusQ4,
		    q3minusQ2,
		    q3plusQ4,
		    quadrant1,
		    quadrant2,
		    quadrant3,
		    quadrant4,
		    reverse,
		    total,
		    totalByPhase
		};
	};

	module phasecode {
		enum PhaseCodeKind {
		    A,
		    AB,
		    ABC,
		    ABCN,
		    ABN,
		    AC,
		    ACN,
		    AN,
		    B,
		    BC,
		    BCN,
		    BN,
		    C,
		    CN,
		    N,
		    none,
		    s1,
		    s12,
		    s12N,
		    s1N,
		    s2,
		    s2N
		};
	};

	struct BasicIntervalSchedule {
		DateTimeType startTime;   
		unitmultiplier::UnitMultiplierKind value1Multiplier;   
		unitsymbol::UnitSymbolKind value1Unit;   
		unitmultiplier::UnitMultiplierKind value2Multiplier;   
		unitsymbol::UnitSymbolKind value2Unit;   
	};
	//@top-level false

	struct DateTimeInterval {
		DateTimeType end;  
		DateTimeType start;  
	};
	//@top-level false

	struct SetPoint {
		string controlType;  
		unitmultiplier::UnitMultiplierKind multiplier;  
		unitsymbol::UnitSymbolKind unit;  
		float value;  
	};
	//@top-level false

	struct ReadingType {
		unitmultiplier::UnitMultiplierKind multiplier;  
		string name;  
		unitsymbol::UnitSymbolKind unit;  
		flowdirection::FlowDirectionKind flowDirection;  
		phasecode::PhaseCodeKind phases;  
	};
	//@top-level false

	struct Reading {
		string source;  
		float value;
		HexBinary16Type qualityFlag;  
		ReadingType readingType;
		DateTimeInterval timePeriod;  
	};
	//@top-level false

	struct Container {
		string logicalDeviceID;
		DateTimeType timestamp; 
	};
	//@top-level false

	enum CurveStyleKind {
	    constantYValue,
	    straightLineYValues
	};

	// =================
	// UNUSED
	// =================
	struct Curve {
		UuidType mRID;   
		CurveStyleKind curveStyle;   
		string description;   
		string name;   
		unitmultiplier::UnitMultiplierKind xMultiplier;   
		unitsymbol::UnitSymbolKind xUnit;   
		unitmultiplier::UnitMultiplierKind y1Multiplier;   
		unitsymbol::UnitSymbolKind y1Unit;   
		unitmultiplier::UnitMultiplierKind y2Multiplier;   
		unitsymbol::UnitSymbolKind y2Unit;   
		unitmultiplier::UnitMultiplierKind y3Multiplier;   
		unitsymbol::UnitSymbolKind y3Unit;   
	};
	//@top-level false

	// =================
	// UNUSED
	// =================
	struct CurveData {
		float xvalue;   
		Curve curve; 
		float y1value;   
		float y2value;   
		float y3value;   
	};
	//@top-level false

	struct EndDeviceControlType {
		string action;  
		string type;  
	};
	//@top-level false

	const unsigned long MaxLengthEndDeviceControlSetPoints = 100;
	typedef sequence<SetPoint, MaxLengthEndDeviceControlSetPoints> SequenceOfEndDeviceControlSetPoints;

	struct EndDeviceControl {
		string issueID;   
		string name;   
		EndDeviceControlType endDeviceControlType;   
		DateTimeInterval scheduledInterval;   
		SequenceOfEndDeviceControlSetPoints setPoints;   
	};
	//@top-level false

	struct Event {
		UuidType mRID;   
		string description;   
		string name;   
		DateTimeType timestamp;   
		string type;   
		string value;   
	};
	//@top-level false

	// =================
	// UNUSED
	// =================
	struct IdentifiedObject {
		UuidType mRID;   
		string description;   
		string name;   
	};
	//@top-level false

	struct PowerSystemResource {
		UuidType mRID;  
		string description;  
		string name;  
	};
	//@top-level false

	struct IrregularTimePoint {
		float value1;  
		float value2;  
	};
	//@top-level false

	const unsigned long MaxLengthForecastScheduleIrregularTimePoints = 100;
	typedef sequence<IrregularTimePoint, MaxLengthForecastScheduleIrregularTimePoints> SequenceOfForecastScheduleIrregularTimePoints;

	struct ForecastSchedule {
		DateTimeType startTime;   
		unitmultiplier::UnitMultiplierKind value1Multiplier;   
		unitsymbol::UnitSymbolKind value1Unit;   
		unitmultiplier::UnitMultiplierKind value2Multiplier;   
		unitsymbol::UnitSymbolKind value2Unit;   
		string version;   
		DateTimeType versionDateTime;   
		SequenceOfForecastScheduleIrregularTimePoints irregularTimePoints;
	};
	//@top-level false

	// =================
	// UNUSED
	// =================
	struct Status {
		DateTimeType timestamp;   
		string value;   
		HexBinary16Type qualityFlag;   
	};
	//@top-level false
	
	struct StringMeasurementValue {
		string value;
		DateTimeType timestamp;   
		HexBinary16Type qualityFlag;   
		string source;
	};
	//@top-level false
	
	struct StringMeasurement {
		UuidType mRID;  
		string description;
		string measurementType;
		string name;
		phasecode::PhaseCodeKind phases;  
		unitsymbol::UnitSymbolKind unit;  
		unitmultiplier::UnitMultiplierKind multiplier;  
		StringMeasurementValue value;
	};
	//@top-level false
	
	const unsigned long MaxLengthStringMeasurements = 100;
	typedef sequence<StringMeasurement, MaxLengthStringMeasurements> SequenceOfStringMeasurements;
	
};

module batterymodule {

	struct BatterySystem {
		commonmodule::UuidType mRID;  
		string description;  
		string name;  
	};
	//@top-level false

	const unsigned long MaxLengthBatterySystemControlSetPoints = 100;
	typedef sequence<commonmodule::SetPoint, MaxLengthBatterySystemControlSetPoints> SequenceOfBatterySystemControlSetPoints;

	struct BatterySystemControl {
		boolean isIslanded;  
		string issueID;  
		string name;  
		commonmodule::EndDeviceControlType endDeviceControlType;
		commonmodule::DateTimeInterval scheduledInterval;  
		SequenceOfBatterySystemControlSetPoints setPoints;  
	};
	//@top-level false

	struct BatteryControlProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		BatterySystem batterySystem; 
		BatterySystemControl batterySystemControl; 
	};
	#pragma keylist BatteryControlProfile logicalDeviceID

	struct BatteryStatus {
		boolean isCharging;  
		boolean isConnected;  
		string mode;  
		float stateOfCharge;  
		commonmodule::DateTimeType timestamp;  
		string value;  
		commonmodule::HexBinary16Type qualityFlag;  
	};
	//@top-level false

	struct BatteryEventProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		BatteryStatus batteryStatus; 
		BatterySystem batterySystem; 
	};
	#pragma keylist BatteryEventProfile logicalDeviceID

	const unsigned long MaxLengthBatteryReadings = 100;
	typedef sequence<commonmodule::Reading, MaxLengthBatteryReadings> SequenceOfBatteryReadings;

	struct BatteryReadingProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		BatterySystem batterySystem; 
		SequenceOfBatteryReadings readings; 
	};
	#pragma keylist BatteryReadingProfile logicalDeviceID


};

module generationmodule {

	struct ActivePower {
		commonmodule::unitmultiplier::UnitMultiplierKind multiplier;   
		commonmodule::unitsymbol::UnitSymbolKind unit;   
		float value;   
	};
	//@top-level false

	struct GeneratingUnit {
		commonmodule::UuidType mRID;   
		string description;   
		string name;   
		ActivePower maxOperatingP;   
	};
	//@top-level false

	const unsigned long MaxLengthGenerationControlSetPoints = 100;
	typedef sequence<commonmodule::SetPoint, MaxLengthGenerationControlSetPoints> SequenceOfGenerationControlSetPoints;

	struct GenerationControl {
		string issueID;   
		string name;   
		commonmodule::EndDeviceControlType endDeviceControlType;   
		commonmodule::DateTimeInterval scheduledInterval;   
		SequenceOfGenerationControlSetPoints setPoints;   
	};
	//@top-level false

	struct GenerationControlProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		GeneratingUnit generatingUnit; 
		GenerationControl generationControl; 
	};
	#pragma keylist GenerationControlProfile logicalDeviceID

	struct GenerationStatus {
		boolean isAutoOn;  
		boolean isConnected;  
		commonmodule::DateTimeType timestamp;  
		string value;  
		commonmodule::HexBinary16Type qualityFlag;  
	};
	//@top-level false

	struct GenerationEventProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		GeneratingUnit generatingUnit; 
		GenerationStatus generationStatus; 
	};
	#pragma keylist GenerationEventProfile logicalDeviceID

	struct GenerationForecastProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		commonmodule::ForecastSchedule forecastSchedule; 
		GeneratingUnit generatingUnit; 
	};
	#pragma keylist GenerationForecastProfile logicalDeviceID

	const unsigned long MaxLengthGenerationReadings = 100;
	typedef sequence<commonmodule::Reading, MaxLengthGenerationReadings> SequenceOfGenerationReadings;

	struct GenerationReadingProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		GeneratingUnit generatingUnit; 
		SequenceOfGenerationReadings readings;
	};
	#pragma keylist GenerationReadingProfile logicalDeviceID

};

module loadmodule {

	struct EnergyConsumer {
		commonmodule::UuidType mRID;   
		string description;   
		string name;   
		string operatingLimit;   
	};
	//@top-level false

	const unsigned long MaxLengthLoadControlSetPoints = 100;
	typedef sequence<commonmodule::SetPoint, MaxLengthLoadControlSetPoints> SequenceOfLoadControlSetPoints;

	struct LoadControl {
		string issueID;   
		string name;   
		commonmodule::EndDeviceControlType endDeviceControlType;   
		commonmodule::DateTimeInterval scheduledInterval;   
		SequenceOfLoadControlSetPoints setPoints;
	};
	//@top-level false

	struct LoadControlProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		EnergyConsumer energyConsumer; 
		LoadControl loadControl; 
	};
	#pragma keylist LoadControlProfile logicalDeviceID

	struct LoadForecastProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		EnergyConsumer energyConsumer; 
		commonmodule::ForecastSchedule forecastSchedule; 
	};
	#pragma keylist LoadForecastProfile logicalDeviceID

	const unsigned long MaxLengthLoadReadings = 100;
	typedef sequence<commonmodule::Reading, MaxLengthLoadReadings> SequenceOfLoadReadings;

	struct LoadReadingProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		EnergyConsumer energyConsumer; 
		SequenceOfLoadReadings readings;
	};
	#pragma keylist LoadReadingProfile logicalDeviceID

	struct LoadStatusProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		EnergyConsumer energyConsumer; 
		commonmodule::SequenceOfStringMeasurements measurements;
	};
	#pragma keylist LoadStatusProfile logicalDeviceID
	
};

module marketmodule {

	enum InterTieDirectionKind {
	    E,
	    I,
	    NETZERO
	};

	enum EnergyProductTypeKind {
	    DYN,
	    FIRM,
	    NFRM,
	    WHL
	};

	enum ExecutionTypeKind {
	    DA,
	    HASP,
	    RTD,
	    RTPD
	};

	enum MarketProductTypeKind {
	    EN,
	    LFU,
	    NR,
	    RC,
	    RD,
	    REG,
	    RU,
	    SR
	};

	enum MarketTypeKind {
	    DAM,
	    HAM,
	    RTM,
	    RUC
	};

	struct MarketFactors {
		commonmodule::DateTimeType intervalEndTime;  
		commonmodule::DateTimeType intervalStartTime;  
	};
	//@top-level false

	struct MarketRun {
		ExecutionTypeKind executionType;
		commonmodule::DateTimeType marketEndTime;  
		string marketID;  
		string marketRunID;  
		commonmodule::DateTimeType marketStartTime;  
		MarketTypeKind marketType;  
	};
	//@top-level false

	const unsigned long MaxLengthOptimizedMicroGridMarketMarketFactors = 100;
	typedef sequence<MarketFactors, MaxLengthOptimizedMicroGridMarketMarketFactors> SequenceOfOptimizedMicroGridMarketMarketFactors;

	const unsigned long MaxLengthOptimizedMicroGridMarketMarketRuns = 100;
	typedef sequence<MarketRun, MaxLengthOptimizedMicroGridMarketMarketRuns> SequenceOfOptimizedMicroGridMarketMarketRuns;

	struct OptimizedMicroGridMarket {
		commonmodule::UuidType mRID;  
		commonmodule::DateTimeType actualEnd;  
		commonmodule::DateTimeType actualStart;  
		string description;  
		boolean dst;  
		commonmodule::DateTimeType end;  
		string localTimeZone;  
		string name;  
		commonmodule::DateTimeType start;  
		string status;  
		float timeIntervalLength;  
		commonmodule::DateTimeType tradingDay;  
		string tradingPeriod;  
		SequenceOfOptimizedMicroGridMarketMarketFactors marketFactors;
		SequenceOfOptimizedMicroGridMarketMarketRuns marketRuns;
	};
	//@top-level false

	struct InterchangeSchedule {
		commonmodule::UuidType mRID;   
		commonmodule::CurveStyleKind curveStyle;   
		string description;   
		InterTieDirectionKind directionType;   
		MarketProductTypeKind energyType;
		long intervalLength;   
		string name;   
		EnergyProductTypeKind scheduleType;
		commonmodule::unitmultiplier::UnitMultiplierKind xMultiplier;   
		commonmodule::unitsymbol::UnitSymbolKind xUnit;   
		commonmodule::unitmultiplier::UnitMultiplierKind y1Multiplier;   
		commonmodule::unitsymbol::UnitSymbolKind y1Unit;   
		commonmodule::unitmultiplier::UnitMultiplierKind y2Multiplier;   
		commonmodule::unitsymbol::UnitSymbolKind y2Unit;   
		commonmodule::unitmultiplier::UnitMultiplierKind y3Multiplier;   
		commonmodule::unitsymbol::UnitSymbolKind y3Unit;   
		OptimizedMicroGridMarket optimizedMicroGridMarket;   
	};
	//@top-level false

	struct InterchangeScheduleProfile {
		string scheduleID; //@key
		commonmodule::DateTimeType timestamp; 
		InterchangeSchedule interchangeSchedule; 
		commonmodule::PowerSystemResource powerSystemResource; 
	};
	#pragma keylist InterchangeScheduleProfile scheduleID

	const unsigned long MaxLengthMarketMarketFactors = 100;
	typedef sequence<MarketFactors, MaxLengthMarketMarketFactors> SequenceOfMarketMarketFactors;

	const unsigned long MaxLengthMarketMarketRuns = 100;
	typedef sequence<MarketRun, MaxLengthMarketMarketRuns> SequenceOfMarketMarketRuns;

	struct Market {
		commonmodule::UuidType mRID;   
		commonmodule::DateTimeType actualEnd;   
		commonmodule::DateTimeType actualStart;   
		string description;   
		boolean dst;   
		commonmodule::DateTimeType end;   
		string localTimeZone;   
		string name;   
		commonmodule::DateTimeType start;   
		string status;   
		float timeIntervalLength;   
		commonmodule::DateTimeType tradingDay;   
		string tradingPeriod;   
		SequenceOfMarketMarketFactors marketFactors;
		SequenceOfMarketMarketRuns marketRuns;
	};
	//@top-level false

};

module reclosermodule {

	enum SwitchStatusKind {
	    Closed,
	    Open
	};

	struct Recloser {
		commonmodule::UuidType mRID;   
		string description;   
		string name;   
		boolean normalOpen;   
	};
	//@top-level false

	const unsigned long MaxLengthRecloserControlSetPoints = 100;
	typedef sequence<commonmodule::SetPoint, MaxLengthRecloserControlSetPoints> SequenceOfRecloserControlSetPoints;

	struct RecloserControl {
		string issueID;
		string name;
		commonmodule::EndDeviceControlType endDeviceControlType;
		commonmodule::DateTimeInterval scheduledInterval;
		SequenceOfRecloserControlSetPoints setPoints;
	};
	//@top-level false

	struct RecloserControlProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		Recloser recloser; 
		RecloserControl recloserControl; 
	};
	#pragma keylist RecloserControlProfile logicalDeviceID

	struct RecloserStatus {
		boolean isBlocked;
		SwitchStatusKind switchStatus;
		commonmodule::DateTimeType timestamp;
		string value;
		commonmodule::HexBinary16Type qualityFlag;
	};
	//@top-level false

	struct RecloserEventProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		Recloser recloser; 
		RecloserStatus recloserStatus; 
	};
	#pragma keylist RecloserEventProfile logicalDeviceID

	const unsigned long MaxLengthRecloserReadings = 100;
	typedef sequence<commonmodule::Reading, MaxLengthRecloserReadings> SequenceOfRecloserReadings;

	struct RecloserReadingProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		Recloser recloser; 
		SequenceOfRecloserReadings readings; 
	};
	#pragma keylist RecloserReadingProfile logicalDeviceID

};

module resourcemodule {

	struct Meter {
		commonmodule::UuidType mRID;   
		string description;   
		string name;   
		commonmodule::PowerSystemResource powerSystemResource;   
	};
	//@top-level false

	const unsigned long MaxLengthResourceReadings = 100;
	typedef sequence<commonmodule::Reading, MaxLengthResourceReadings> SequenceOfResourceReadings;

	struct ResourceReadingProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		Meter meter; 
		SequenceOfResourceReadings readings;
	};
	#pragma keylist ResourceReadingProfile logicalDeviceID
	
	struct ResourceStatusProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		commonmodule::PowerSystemResource powerSystemResource;   
		commonmodule::SequenceOfStringMeasurements measurements;
	};
	#pragma keylist ResourceStatusProfile logicalDeviceID
	
};

module securitymodule {

	struct SecurityEvent {
		commonmodule::UuidType mRID;   
		string description;   
		string log;   
		string name;   
		string severity;   
		commonmodule::DateTimeType timestamp;   
		string type;   
		string value;   
	};
	//@top-level false

	struct SecurityEventProfile {
		string logicalDeviceID; //@key 
		commonmodule::DateTimeType timestamp; 
		SecurityEvent securityEvent; 
	};
	#pragma keylist SecurityEventProfile logicalDeviceID

};

module solarmodule {

	struct SolarCapability {
		float ahrRtg;   
		string qualityFlag;   
		commonmodule::DateTimeType timestamp;
		float voltage;
		float wRtgMaxVal;   
		float wRtgMinVal;   
	};
	//@top-level false

	struct SolarInverter {
		commonmodule::UuidType mRID;  
		string description;  
		string name;  
	};
	//@top-level false

	struct SolarInverterStatus {
		boolean isConnected;  
		commonmodule::DateTimeType timestamp;  
		string value;  
		commonmodule::HexBinary16Type qualityFlag;  
	};
	//@top-level false

	struct SolarCapabilityProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		SolarCapability solarCapability; 
		SolarInverter solarInverter; 
	};
	#pragma keylist SolarCapabilityProfile logicalDeviceID

	const unsigned long MaxLengthSolarControlSetPoints = 100;
	typedef sequence<commonmodule::SetPoint, MaxLengthSolarControlSetPoints> SequenceOfSolarControlSetPoints;

	struct SolarControl {
		boolean isIslanded;   
		string issueID;   
		string name;   
		commonmodule::EndDeviceControlType endDeviceControlType;   
		commonmodule::DateTimeInterval scheduledInterval;   
		SequenceOfSolarControlSetPoints setPoints;   
	};
	//@top-level false

	struct SolarControlProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp;
		SolarControl solarControl; 
		SolarInverter solarInverter; 
	};
	#pragma keylist SolarControlProfile logicalDeviceID

	struct SolarEventProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		SolarInverter solarInverter; 
		SolarInverterStatus solarInverterStatus; 
	};
	#pragma keylist SolarEventProfile logicalDeviceID

	struct SolarForecastProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		commonmodule::ForecastSchedule forecastSchedule; 
		SolarInverter solarInverter;
	};
	#pragma keylist SolarForecastProfile logicalDeviceID

	const unsigned long MaxLengthSolarReadings = 100;
	typedef sequence<commonmodule::Reading, MaxLengthSolarReadings> SequenceOfSolarReadings;

	struct SolarReadingProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		SequenceOfSolarReadings readings; 
		SolarInverter solarInverter; 
	};
	#pragma keylist SolarReadingProfile logicalDeviceID

};

module weathermodule {
	struct HumidityData {
		commonmodule::DateTimeType timestamp;
		float value;
	};
	//@top-level false
	
	const unsigned long MaxLengthHumidityData = 100;
	typedef sequence<HumidityData, MaxLengthHumidityData> SequenceOfHumidityData;
	
	struct Humidity {
		commonmodule::unitsymbol::UnitSymbolKind unit;
		SequenceOfHumidityData humidityData;
	};
	//@top-level false
	
	struct SunRadiationData {
		commonmodule::DateTimeType timestamp;
		float value;
	};
	//@top-level false
	
	const unsigned long MaxLengthSunRadiationData = 100;
	typedef sequence<SunRadiationData, MaxLengthSunRadiationData> SequenceOfSunRadiationData;
	
	struct SunRadiation {
		commonmodule::unitmultiplier::UnitMultiplierKind multiplier;
		commonmodule::unitsymbol::UnitSymbolKind unit;
		SequenceOfSunRadiationData sunRadiationData;
	};
	//@top-level false
	
	struct TemperatureData {
		commonmodule::DateTimeType timestamp;
		float value;
	};
	//@top-level false
	
	const unsigned long MaxLengthTemperatureData = 100;
	typedef sequence<TemperatureData, MaxLengthTemperatureData> SequenceOfTemperatureData;
	
	struct Temperature {
		commonmodule::unitsymbol::UnitSymbolKind unit;
		SequenceOfTemperatureData temperatureData;
	};
	//@top-level false
	
	struct WindData {
		commonmodule::DateTimeType timestamp;
		float windDirection;
		float windSpeed;
	};
	//@top-level false
	
	const unsigned long MaxLengthWindData = 100;
	typedef sequence<WindData, MaxLengthWindData> SequenceOfWindData;
	
	struct Wind {
		commonmodule::unitsymbol::UnitSymbolKind directionUnit;
		commonmodule::unitsymbol::UnitSymbolKind speedUnit;
		SequenceOfWindData windData;
	};
	//@top-level false
	
	struct WeatherData {
		string version;
		commonmodule::DateTimeType versionDateTime;
		Humidity humidity;
		SunRadiation sunRadiation;
		Temperature temperature;
		Wind wind;
	};
	//@top-level false

	struct WeatherDataProfile {
		string logicalDeviceID; //@key
		commonmodule::DateTimeType timestamp; 
		WeatherData weatherData;
	};
	#pragma keylist WeatherDataProfile logicalDeviceID

};

}; // openfmb